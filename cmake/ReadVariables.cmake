# Simple CMake utility to read variables from MK files
#   - Gets contents from given file (name or path)
#   - Parses the assignment statements
#   - Makes the same assignments in the PARENT_SCOPE


function(ReadVariables MKFile)
    file(READ "${MKFile}" FileContents)
    string(REPLACE "\\\n" "" FileContents ${FileContents})
    string(REPLACE "\n" ";" FileLines ${FileContents})
    list(REMOVE_ITEM FileLines "")
    foreach(line ${FileLines})
        if(${line} MATCHES "#")
            continue()
        endif()
        string(REPLACE "=" ";" line_split ${line})
        list(LENGTH line_split count)
        if (count LESS 2)
            continue()
        endif()
        list(GET line_split -1 value)
        if("${value}" STREQUAL "")
            continue()
        endif()
        string(STRIP ${value} value)
        separate_arguments(value)
        if("${value}" STREQUAL "")
            continue()
        endif()
        list(REMOVE_AT line_split -1)
        string(REPLACE "-l" " -l" value ${value})
        foreach(var_name ${line_split})
            string(STRIP ${var_name} var_name)
            set(${var_name} ${value} PARENT_SCOPE)
        endforeach()
    endforeach()
endfunction()